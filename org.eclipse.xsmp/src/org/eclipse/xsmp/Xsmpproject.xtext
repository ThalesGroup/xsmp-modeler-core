/*******************************************************************************
* Copyright (C) 2020-2023 THALES ALENIA SPACE FRANCE.
*
* All rights reserved. This program and the accompanying materials
* are made available under the terms of the Eclipse Public License 2.0
* which accompanies this distribution, and is available at
* https://www.eclipse.org/legal/epl-2.0/
*
* SPDX-License-Identifier: EPL-2.0
******************************************************************************/

grammar org.eclipse.xsmp.Xsmpproject with org.eclipse.xtext.common.Terminals hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://org.eclipse.xsmp/xsmp"

Project:
	metadatum=Metadatum
	'project' name=STRING
	((tools+=ToolReference | sources+=SourcePath | referencedProjects+=ProjectReference | includes+=IncludePath)*
	profile=ProfileReference)?
	(tools+=ToolReference | sources+=SourcePath | referencedProjects+=ProjectReference | includes+=IncludePath)*;

Profile:
	metadatum=Metadatum
	'profile' name=STRING;

Tool:
	metadatum=Metadatum
	'tool' name=STRING;

ProfileReference:
	'profile' profile=[Profile|STRING];

ToolReference:
	'tool' tool=[Tool|STRING];

SourcePath:
	'source' name=STRING;

IncludePath returns SourcePath:
	'include' name=STRING;

ProjectReference:
	'dependency' project=[Project|STRING];

Metadatum:
	{Metadatum}
	documentation=ML_DOCUMENTATION?;

terminal ML_DOCUMENTATION:
	'/**' (!('*' | '/') .*)? '*/';

@Override
terminal ML_COMMENT:
	'/*' (!'*' .* | '*' '*'+ (!('/' | '*') .*)?)? '*/';
