/*******************************************************************************
* Copyright (C) 2020-2023 THALES ALENIA SPACE FRANCE.
*
* All rights reserved. This program and the accompanying materials
* are made available under the terms of the Eclipse Public License 2.0
* which accompanies this distribution, and is available at
* https://www.eclipse.org/legal/epl-2.0/
*
* SPDX-License-Identifier: EPL-2.0
******************************************************************************/
module org.eclipse.xsmp.GenerateXsmp

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.emf.ecore.resource.impl.ResourceSetImpl
import org.eclipse.emf.mwe.utils.*

var rootPath = ".."
var runtimeProjectName = "org.eclipse.xsmp"

Workflow {

    bean = StandaloneSetup {
        platformUri = "${rootPath}"
        scanClassPath = true
        registerGenModelFile = "platform:/resource/${runtimeProjectName}/model/xsmp.genmodel"
        registerEcoreFile = "platform:/resource/${runtimeProjectName}/model/xsmp.ecore"
    }

    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = "org.eclipse.xsmp"
                rootPath = rootPath
                runtimeTest = {
                    enabled = true
                }
                eclipsePlugin = {
                    enabled = true
                }
                eclipsePluginTest = {
                    enabled = true
                }
                createEclipseMetaData = true
            }
            code = {
                encoding = "UTF-8"
                lineDelimiter = "\n"
                fileHeader =
                "/*******************************************************************************\n* Copyright (C) 2020-\${year} THALES ALENIA SPACE FRANCE.\n*\n* All rights reserved. This program and the accompanying materials\n* are made available under the terms of the Eclipse Public License 2.0\n* which accompanies this distribution, and is available at\n* https://www.eclipse.org/legal/epl-2.0/\n*\n* SPDX-License-Identifier: EPL-2.0\n******************************************************************************/\n"
                preferXtendStubs = false
            }
        }

        language = StandardLanguage {
            name = "org.eclipse.xsmp.Xsmpcore"
            fileExtensions = "xsmpcore"
            serializer = {
                generateStub = false
            }
            generator = {
                generateStub = false
            }
            scopeProvider = {
                generateStub = true
            }

            fragment = ui.codemining.CodeMiningFragment {
                generateStub = false
            }

            formatter = {
                generateStub = true
            }

            junitSupport = {
                generateStub = false
            }
            projectWizard = {
                generate = false
            }
            labelProvider = {
                generateStub = false
            }
            outline = {
                generateStub = false
            }
            quickFixProvider = {
                generateStub = false
            }
            contentAssist = {
                generateStub = false
            }
        }

        language = StandardLanguage {
            name = "org.eclipse.xsmp.Xsmpproject"
            fileExtensions = "project"
            referencedResource = "platform:/resource/${runtimeProjectName}/src/org/eclipse/xsmp/Xsmpcore.xtext"
            serializer = {
                generateStub = false
            }

            qualifiedNamesProvider = {}
            scopeProvider = {
                generateStub = true
            }
            formatter = {
                generateStub = true
            }
            generator = {
                generateStub = false
            }
            fragment = ui.codemining.CodeMiningFragment {
                generateStub = false
            }

            junitSupport = {
                junitVersion = "5"
            }
            projectWizard = {
                generate = false
            }
            labelProvider = {
                generateStub = false
            }
            outline = {
                generateStub = false
            }
            quickFixProvider = {
                generateStub = false
            }
        }
        language = StandardLanguage {
            name = "org.eclipse.xsmp.Xsmpcat"
            fileExtensions = "xsmpcat"
            referencedResource = "platform:/resource/${runtimeProjectName}/src/org/eclipse/xsmp/Xsmpcore.xtext"
            serializer = {
                generateStub = false
            }

            qualifiedNamesProvider = {}
            scopeProvider = {
                generateStub = true
            }
            formatter = {
                generateStub = true
            }

            fragment = ui.codemining.CodeMiningFragment {
                generateStub = true
            }

            junitSupport = {
                junitVersion = "5"
            }
            projectWizard = {
                generate = true
            }
        }

    }
    component = org.eclipse.emf.mwe2.ecore.EcoreGenerator auto-inject {
        genModel = "platform:/resource/${runtimeProjectName}/model/xsmp.genmodel"
        srcPath = "platform:/resource/${runtimeProjectName}/src"
    }
}
