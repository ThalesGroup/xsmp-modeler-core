plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    api project(':org.eclipse.xsmp')

}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
application {
    mainClass = "org.eclipse.xsmp.tool.smp.cli.SmpCli"
}

shadowJar {
    from(project.extensions.findByType(JavaPluginExtension.class).sourceSets.main.output)
    configurations = [project.configurations.runtimeClasspath]
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA','schema/*',
        '.options', '.api_description', '*.profile', '*.html', 'about.*', 'about_files/*',
        'plugin.xml', 'systembundle.properties', 'profile.list')
    archiveClassifier = 'cli'
    archiveVersion = ''
    append('plugin.properties')
    mergeServiceFiles()
}



configurations {
    mwe2 {
        extendsFrom compileClasspath
    }
}

dependencies {
    mwe2 'org.eclipse.emf:org.eclipse.emf.mwe2.launch'
    mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}"
    mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"
    mwe2 'org.eclipse.xtext:xtext-antlr-generator'
}

task generateXtextLanguage(type: JavaExec) {
    mainClass = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = configurations.mwe2
    inputs.file "src/org/eclipse/xsmp//tool/smp/GenerateSmp.mwe2"
    inputs.file "model/smp.genmodel"
    inputs.file "model/catalogue.ecore"
    inputs.file "model/configuration.ecore"
    inputs.file "model/elements.ecore"
    inputs.file "model/package.ecore"
    inputs.file "model/types.ecore"
    inputs.file "model/xlink.ecore"
    outputs.dir "src-gen"
    args += "src/org/eclipse/xsmp//tool/smp/GenerateSmp.mwe2"
    args += "-p"
    args += "rootPath=/${projectDir}/.."
}

processResources.dependsOn(generateXtextLanguage)
generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]

//this is an eclipse plugin project
eclipseClasspath.enabled=false
cleanEclipseClasspath.enabled=false
